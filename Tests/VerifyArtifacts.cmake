if(NOT DEFINED MP3GLITCH_STAGED_DIR)
    message(FATAL_ERROR "MP3GLITCH_STAGED_DIR is required")
endif()

set(vst3 "${MP3GLITCH_STAGED_DIR}/VST3/MP3 Glitch.vst3")
if(MP3GLITCH_EXPECT_AU)
    set(standalone "${MP3GLITCH_STAGED_DIR}/Standalone/MP3 Glitch.app")
elseif(WIN32)
    set(standalone "${MP3GLITCH_STAGED_DIR}/Standalone/MP3 Glitch.exe")
else()
    set(standalone "${MP3GLITCH_STAGED_DIR}/Standalone/MP3 Glitch")
endif()

if(NOT EXISTS "${vst3}")
    message(FATAL_ERROR "Missing staged VST3: ${vst3}")
endif()

if(NOT EXISTS "${standalone}")
    message(FATAL_ERROR "Missing staged Standalone app: ${standalone}")
endif()

if(MP3GLITCH_EXPECT_AU)
    set(module_info "${vst3}/Contents/Resources/moduleinfo.json")
    set(standalone_plist "${standalone}/Contents/Info.plist")
    set(au "${MP3GLITCH_STAGED_DIR}/AU/MP3 Glitch.component")

    if(NOT EXISTS "${module_info}")
        message(FATAL_ERROR "Missing staged VST3 moduleinfo.json: ${module_info}")
    endif()

    if(NOT EXISTS "${standalone_plist}")
        message(FATAL_ERROR "Missing staged Standalone Info.plist: ${standalone_plist}")
    endif()

    if(NOT EXISTS "${au}")
        message(FATAL_ERROR "Missing staged AU: ${au}")
    endif()

    foreach(bundle IN ITEMS "${vst3}" "${standalone}" "${au}")
        execute_process(
            COMMAND codesign --verify --deep --strict "${bundle}"
            RESULT_VARIABLE codesign_result
            ERROR_VARIABLE codesign_error)
        if(NOT codesign_result EQUAL 0)
            message(FATAL_ERROR "Invalid signature for ${bundle}: ${codesign_error}")
        endif()
    endforeach()
endif()
